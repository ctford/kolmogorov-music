% Based on the template for sigplanconf LaTeX Class by Paul C. Anagnostopoulos (paul@windfall.com)
\documentclass[numbers]{sigplanconf}

\usepackage{amsmath}
\usepackage{hyperref}

\newcommand{\cL}{{\cal L}}

\begin{document}

\special{papersize=8.5in,11in}
\setlength{\pdfpageheight}{\paperheight}
\setlength{\pdfpagewidth}{\paperwidth}

\conferenceinfo{FARM 2016}{September 24, 2016, Nara, Japan}
\copyrightyear{2016}
%\copyrightdata{978-1-nnnn-nnnn-n/yy/mm}
%\copyrightdoi{nnnnnnn.nnnnnnn}

\publicationrights{author-pays}

\titlebanner{Submission for FARM 2016}

\title{Algomusicology, ????, Profit}
%\subtitle{Subtitle Text, if any}

\authorinfo{Chris Ford}
           {ThoughtWorks}
           {christophertford@gmail.com}

\maketitle

\begin{abstract}
In this paper I propose a novel method for converting algomusicology into profit. While my particular example
is tongue-in-cheek, it shows that algorithmic composition and analysis can help us deconstruct and disrupt
traditional notions of musical authorship.
\end{abstract}

%\category{CR-number}{subcategory}{third-level}

%\keywords
%keyword1, keyword2

\section{Introduction}

The South Park Underpants Gnomes\cite{Gnomes} are renowned for their business plan:
\begin{enumerate}
    \item Collect underpants
    \item ????
    \item Profit
\end{enumerate}

A common interpretation of the Gnomes' business is as a satire on Silicon Valley startups.
Algomusicologists and starter-uppers have a challenge in common with the Underpants Gnomes
- we may have collected and analysed a considerable quantity of underpants/music/users, but how are we to profit from that?

Less glibly, is algomusicology doomed to be a mere observer and explainer of musical practice, or might it become an
active participant in the conversation? Computer science is able to influence the software industry, though that might take longer
than we'd like.~\footnote{Alonzo Church developed the lambda calculus in the 1930s. Java gained lambda expressions in 2015.}
Can algomusicology influence music?~\footnote{This paper appears to be the first reference to algomusicology. I use the term,
which is a portmanteau of algorithm and musicology similar to algorave, to describe the use of information theory and programming for musical analysis.}

This paper will argue that it can. It will do so by reviewing existing literary and musical works that are informed by algorithmic
thinking, and also by using algomusicological concepts to create a new work that challenges our present conception of musical
authorship and copyright. As befits its subject matter, this paper will incorporate conventions and style from literary essays
and musicology papers.

The examples in this paper are all written in Clojure\cite{Clojure}, a dialect of Lisp. I also rely on Leipzig\cite{Leipzig},
a music theory library, and Midje\cite{Midje}, a unit testing library.

\section{Air on the {\textbackslash}G String}
Very great quantities of data can exhibit very little complexity. Consider the following Clojure expression, which
evaluates to a sequence of $10^{15}$ uppercase G characters:

\begin{verbatim}
(repeat (* 1000 1000 1000 1000 1000) \G)
\end{verbatim}

Though storing such a string would require roughly a petabyte of memory, representing it indirectly as a computation
takes very little space at all. That is roughly five times the the size of the entire iTunes library,
described in less than a half a tweet.~\footnote{Based on the the iTunes library featuring approximately 50 million songs
of approximately 4 megabytes each. Tweets are restricted to 140 characters.}

The insight that the inherent complexity of data can be measured by the length of the computer program needed to express it
originates with Andrey Kolmogorov\cite{On Tables of Random Numbers}. The Kolmogorov complexity of a piece of data is defined
as the length of the shortest program that evaluates to it. The greater the structural regularity, the more opportunities
to express the data using a short expression.

We can use Clojure's macro facility to build a crude framework for comparing the size of descriptions and their
resulting values. The following two definitions are identical, save that \verb|description-length| is a macro and therefore
operates on the unevaluated expression.

\begin{verbatim}
(defn result-length [expression]
  (-> expression print-str count))

(defmacro description-length [expression]
  (-> expression print-str count))
\end{verbatim}

Here is an example of using these definitions to calculate the description and result length of a short sequence of Gs.
The \verb|fact| notation is due to Midje, which allows us to write \verb|(fact expression => result)|,
which asserts that \verb|expression| evaluates to \verb|result|.

\begin{verbatim}
(fact
  (result-length (repeat 65 \G)) => 131
  (description-length (repeat 65 \G)) => 13)
\end{verbatim}

The explanatory power of a description can be measured by comparing the length of the textual description and the length
of the resulting value. Explanatory power is equivalent to compression power, where representing a value via an
expression is viewed as a compression strategy.

If there is no way to describe a value more concisely than the value itself, we say that it is random. In other words, an
explanatory power of one or less is no explanation at all. In compression terms, there is no possible space saving
by representing this value indirectly.

The difficulty inherent in this definition is that we have no way of knowing if we have tried the most efficient way of
computing a value or not. There are many ways to compute the same result. Some will be concise. These offer great
explanatory power. Others will be verbose. These offer less explanatory power.

Kolmogorov complexity is therefore uncomputable. Once we have found an expression that produces a value we can establish
an upper bound for its Kolmogorov complexity, but we are unable to prove that we have found the most concise way to
represent it.

Fortunately, our inability to determine the complexity of a particular result does not prevent us from measuring the
explanatory power of a particular description. Better descriptions may exist, but we need not worry about them.

Here is a Clojure macro that calculates the explantory power of an expression as the ratio between the length of its
result and the length of the expression itself.

\begin{verbatim}
(defmacro explanatory-power [expression]
  `(/ (result-length ~expression)
      (description-length ~expression)))
\end{verbatim}

The explanatory power of our description of 65 Gs is great. A repetitive string of identical characters is
highly redundant, highly compressible and hence amenable to concise description. The explanatory power of this way of
representing the string of Gs this way is just over 10, because the description is about a tenth of the length of the
value itself.

\begin{verbatim}
(fact
  (explanatory-power (repeat 65 \G)) => 131/13)
\end{verbatim}

There may be an even more concise way to represent our G string. If so, its Kolmogorov complexity may be even lower than
131/13. However, we know for certain that 131/13 is an upper bound for the complexity of that string.

\section{Analysis by compression}

David Meredith has used Kolmogorov complexity to compare the quality of musical analyses\cite{Analysis by Compression}.
He starts by assuming that musical analysis aims to produce the ``best possible explanation for musical works". If that's the case,
how might we compare two different analyses of the same piece?

Meredith's answer is that by representing an analysis of a piece of music as a computer program, we can use Kolmogov complexity
to objectively measure the quality of the explanation it provides. A short program must necessarily capture a lot of the structure
of a piece, because summarising a piece's structure is the only way to achieve compression. A longer program is a worse explanation,
because it has missed opportunities to exploit structural regularities in the piece.

Describing the notes of a musical work as code is straightforward, as notes can be represented by simple data structures like
sequences of time/pitch tuples. Selecting which encoding scheme to use is less straightforward, because the choice of primitives
can affect the complexity score. Ultimately, the choice of music theory language is difficult, somewhat subjective and
has trade-offs. I will not attempt to address this choice here as it doesn't materially impact my arguments.

This paper uses Leipzig to provide its music theory primitives. Leipzig allows programmers to describe melodies as a sequence
of durations and a sequence of pitches. The following example uses Leipzig's \verb|phrase| function to produces a sequence of
time/pitch/duration tuples.

\begin{verbatim}
  (phrase [3/3  3/3  2/3  1/3  3/3]
          [  0    0    0    1    2])
\end{verbatim}

Individual phrases can be combined together to form larger pieces. Here are two equivalent ways of playing the above melody twice.

\begin{verbatim}
  (->>
    (phrase [3/3  3/3  2/3  1/3  3/3]
            [  0    0    0    1    2])
    (then
      (phrase [3/3  3/3  2/3  1/3  3/3]
              [  0    0    0    1    2])))

\end{verbatim}

And alternatively:

\begin{verbatim}
  (->>
    (phrase [3/3  3/3  2/3  1/3  3/3]
            [  0    0    0    1    2])
    (times 2))
\end{verbatim}

The latter way is considerably shorter. It also demonstrates a greater understanding of the piece. The notes are exactly
repeated twice, so writing them out again longhand is a waste. The first way of writing the notes misses this opportunity to
summarise and is therefore a worse explanation.

One might imagine that a maintainer of this musical code would refactor the former expression into the latter. Viewed this way,
refactoring away repetition can be seen as improving the explanatory power of the code.

Using this idea, we can show that composing ``Row, row, row your boat" out of sequences of durations and pitches
is a better description of the piece than the raw notes. \verb|;| indicates a comment in Clojure.

\begin{verbatim}
(def row-row
              ; Row, row, row  your boat,
  (->> (phrase [3/3  3/3  2/3  1/3  3/3]
               [  0    0    0    1    2])

     (then    ; Gent-ly   down the  stream,
       (phrase [2/3  1/3  2/3  1/3  6/3]
               [  2    1    2    3    4]))

     (then    ; Merrily, merrily, merrily, merrily,
       (phrase (repeat 12 1/3)
               (mapcat #(repeat 3 %) [7 4 2 0])))

     (then    ; Life is   but  a    dream!
       (phrase [2/3  1/3  2/3  1/3  6/3]
                 [  4    3    2    1    0]))

     (canon (simple 4))
     (where :pitch (comp A major))))
\end{verbatim}

We can calculate the ratio between the literal notes of the song and the above description.
The \verb|definitional| macro allows us to use the symbol \verb|row-row| in our fact notation, without
having to paste in the entire expression.

\begin{verbatim}
(defmacro definitional [macro sym]
  (let [value
          (-> sym repl/source-fn read-string last)]
    `(~macro ~value)))

(fact
  (definitional description-length row-row)
    => 281
  (definitional result-length row-row)
    => 2037
  (definitional explanatory-power row-row)
    => 2037/281)
\end{verbatim}

The particularly significant compression in this way of writing ``Row, row, row your boat" is the line \verb|(canon (simple 4))|.
A canon is when an initial melody (often known as the dux) is accompanied by a transformation of itself (often known as the comes).
In ``Row, row, row your boat", the comes is a copy of the dux that is delayed by four beats, a structure known as a simple canon.

Here is the definition of \verb|canon|:

\begin{verbatim}
(defn canon [f notes]
  (->> (f notes) (with notes)))
\end{verbatim}

In a canon, every note has two lives. Firstly, it is played straight. Secondly, it is played again in a modified form according
to the canon's \verb|f|. By understanding this, we can almost halve the length of the program we need to produce the piece.

The more concise the algorithmic description of a piece, the better the musical model.

\section{The Library of Babel}

Music is widely regarded to have mathematical properties, but literature also contains examples of works where artists have engaged
with algorithmic and information theoretical concepts. One striking case is the Library of Babel as imagined by Jorge Luis
Borges\cite{The Library of Babel}. We will examine the story in a moment, but first we must define the mathematical concept
that it revolves around - the Kleene star.

The Kleene star is the set of all possible strings that can be built from a set of elements. Here is a function that takes a set
of elements and enumerates the Kleene star as a lazy sequence. It works by taking the strings generated thus far, appending each
element to each string and then repeating the process. This results in an infinite lazy sequence of strings of ever increasing
length, starting from the empty string.

\begin{verbatim}
(defn kleene* [elements]
  (letfn [(expand [strings]
            (for [s strings e elements]
              (conj s e)))]
    (->>
      (lazy-seq (kleene* elements))
      expand
      (cons []))))
\end{verbatim}

We can use this construction with an ASCII alphabet to obtain all possible ASCII strings. We might just as easily have
used the letters `G', `A', `T' and `C' to generate every possible DNA sequence.

This results in something very similar to Borges' Library of Babel. In this vast fictional library, thousands of hexagonal
rooms house all possible books of four hundred and ten pages. Melancholic librarians spend their entire lives toiling in its
maddiningly symetrical depths.

Some books exhibit apparent sense, most exhibit none. The narrator describes one that is ``a mere labyrinth of letters,
but the next-to-last page says \textit|Oh time thy pyramids|."

Unlike Borges's library, our strings are not limited to four hundred and ten pages. Because of this limit, the Library of Babel
must either be finite, or have identical copies of the same book. Our list is an infinite sequence of all possible strings of
all finite lengths.

\begin{verbatim}
(defn library-of-babel []
  (let [ascii (->> (range 32 127) (map char))]
    (->> ascii
       kleene*
       (map #(apply str %)))))

(fact
  (->> (library-of-babel) (take 3)) => ["" " " "!"]
  (nth (library-of-babel) 364645) => "GEB")
\end{verbatim}

If we enumerate the Kleene star of a particular alphabet, we get a sequence of all its possible strings.
If we concatenate them all together into one long string, it produces what is known as a disjunctive sequence or lexicon.
A lexicon contains every finite string as a substring. It is called a lexicon because it says everything that is possible
to say with that alphabet.

The results of the Kleene star are interesting from a Kolmogorov complexity perspective, because they can be described
concisely yet contain multitudes. Paradoxically, the whole can be simpler than the parts.
That is, the Kolmogorov complexity of one string in our Library of Babel is more complicated to describe than the Library
taken as a whole.

Borges appreciated this paradox. His anonymous narrator reports that on ``some shelf in some hexagon (men reasoned)
there must exist a book which is the formula and perfect compendium of all the rest". In other words, that one book
epitomises the complexity of the entire library, though it is part of the library. Any of the many books that contain our
\verb|library-of-babel| function match the narrator's description, though the books that also contain the rest of this paper
must surely provide more helpful context.

Through his Library of Babel, Borges is playing with our notions of invention versus discovery. Mathematicians and computer
scientists struggle with whether they are the inventors of new theories or discoverers. Philip Wadler, for example,
argues that the lambda calculus is so fundamental that it deserves to be considered a universal concept that
was discovered\cite{Propositions as Types}. By positing a library where any work may be found if you know where to look, Borges
suggests that even literary authorship could be considered a search problem and therefore a matter of discovery rather than
creation.

\section{Contact}

In Carl Sagan's novel `Contact'\cite{Contact}, he takes the concept of discovering meaning in naturally occurring sources of
information even further than Borges. Firstly, Sagan imagines that SETI detects a transmission consisting of a series of prime
numbers from the the Vega star system. This prompts an exploration of how to distinguish between a signal created by an
intelligence and one arising from natural symmetry.

Later a message to humanity is discovered encoded in the digits of pi, placed there by beings who are able to manipulate the
structure of the universe itself. This development questions the dichotomy between invented and discovered information altogether,
by imagining a mathematical quantity as a medium for meaningful communication.

If we design an appropriate encoding scheme, we could do a similar thing and interpret the digits of pi as music. However,
we would encounter a similar problem to Borges' librarians - we suspect that every sequence of data we could possibly want
is in there somewhere, but we don't know where to look.

If pi's decimal expansion does have every possible combination of digits, then it's a lexicon for the decimal digits.
But we don't know for sure. No one has been able to prove it either way.

Fortunately, there are other transcendental numbers that are more predictable. The Champernowne constant was discovered
by David Champernowne as an undergraduate\cite{The construction of decimals normal in the scale of ten}. It has a
decimal expansion that is the concatenation of the digits of the natural numbers. To refer to the sequence of digits
in its decimal expansion we can simply refer to the Champernowne word.

Here we construct the Champernowne word by decomposing each natural number into its constituent digits and concatenating
the results together.

\begin{verbatim}
(defn decompose-into-digits [n]
  (let [[remainder quotient]
          ((juxt mod quot) n 10)]
    (if (zero? quotient)
      [remainder]
      (conj
        (decompose-into-digits quotient)
        remainder))))

(defn champernowne-word
  [skip-to]
  (->> (range)
       (map #(+ skip-to %))
       (mapcat decompose-into-digits)))

(fact
  (->> (champernowne-word 0) (take 16))
    => [0 1 2 3 4 5 6 7 8 9 1 0 1 1 1 2])
\end{verbatim}

It is easily seen to be a lexicon for the decimal digits, because:
\begin{enumerate}
    \item By definition, the Champernowne word includes every natural number.
    \item Every sequence of digits corresponds to a natural number.
    \item Therefore, the Champernowne word contains all possible sequences of digits.
\end{enumerate}
Of course, a sequence of digits may occur earlier than its natural number - the sequence ``34" first occurs when numbers three and
four are encountered, well in advance of the natural number 34.

In the above example we generate the word starting from zero, and decompose the digits of
every subsequent natural number. However, we could just as easily start at any other natural number, which is demonstrated
by the \verb|skip-to| parameter. This is a useful property, because it lets us jump straight to any part of the constant
without having to generate all the previous digits.

\section{Flutter}

Complexity and information theory have been used to speculate on grand questions like the place of humanity in the universe,
but they have also been used by artists to comment on specific social issues. The Criminal Justice and Public Order Act 1994
was an attempt by the UK Government to curb rave culture. The musicological dilemma encountered by the government of the day
was how to define raves. They settled on prohibiting gatherings where ``a succession of repetitive beats" were
played\cite{Joseph Gallivan on pop}.

The duo Autechre responded with the Anti EP\cite{Anti EP}, whose third track `Flutter', was designed to protest the legislation.
Band member
Sean Booth explained that they ``made as many different bars as we could on the drum machine, then strung them all together."
\cite{Joseph Gallivan on pop}
In other words, as every bar features a unique beat, `Flutter' does not match the government's algomusicological description
of ``repetitive beats".

Autechre appreciated the practical difficulties of negotiating the subtleties of complexity theory at 3am in a noisy field,
so they advised DJs to ``have a lawyer and a musicologist present at all times to confirm the non repetitive nature of the
music in the event of police harassment."\cite{Joseph Gallivan on pop}

Both legislators and artists are clearly taking algomusicological positions. The government attempted to construct the set
of all rave musics using a complexity characterisation. Autechre responded with what amounts to a diagonal argument, demonstrating
the inadequacy of the original claim.

\section{Blurred Lines}

We will now take the ideas explored in previous sections and rework them into a novel contribution to the politics of
copyright and creativity.

`Blurred Lines' is a 2013 pop song written by Pharrell Williams and Robin Thicke\cite{Blurred Lines}. In March 2015,
they were found to have infringed on the copyright of the Marvin Gaye song
`Got to Give It Up'\cite{Got to Give It Up, USA Today}.

Such judgements are perilous from a musicological point of view, as they are often based on a jury's intuition as to
the similarity between songs and not, for example, on a conditional Kolmogorov complexity analysis of the distance between
the two compositions.

As a response to this situation, I decided to compose a track that would infringe on the copyright of both songs. The following
piece decodes the Champernowne word into music.

\begin{verbatim}
(defn copyright-infringement-song
  [skip-to]
  (->>
    (champernowne-word skip-to)
    (coding/decode 3)
    (tempo (bpm 120))))
\end{verbatim}

As the Champernowne word's digits are predictable, I am able to skip ahead to any part of the song I choose. Here
is the value of \verb|skip-to| that results in a passable version of `Blurred Lines'.~\footnote{The encoding scheme is chosen
to be reversible, which allowed me to determine where in the Champernowne word `Blurred Lines' would occur. Full details are in the appendix and the accompanying code.}

\begin{verbatim}
(def gaye-williams-thicke-constant
  124500120012003112731276127312761273127...
  612731276127312761273127612450012001212...
  450012001200311273127612731276127312761...
  273127612731276127312761245001200121245...
  001200120031127312761273127612731276127...
  312761273127612731276124500120012124500...
  120012003112731276127312761273127612731...
  276127312761273127612450012001212400012...
  001200311268127112681271126812711268127...
  112681271126812711240001200121240001200...
  120031126812711268127112681271126812711...
  268127112681271124000120012124000120012...
  003112681271126812711268127112681271126...
  812711268127112400012001212520041004112...
  64125012621249126112471245)
\end{verbatim}

Nothing in the the \verb|copyright-infringement-song|'s description is in any way related to `Blurred Lines'.
It is simply a straightforward musical rendering of a well-known mathematical constant derived from the decimal counting
system.

What's more, every other song is also somewhere in there. Past, present and future compositions already exist somewhere in
the depths of this infinite piece.

Here we finally come to the profit generation strategy - copyright litigation. As the \verb|copyright-infringement-song| is
a universal piece of music, any work published subsequent to it infringes on its copyright.~\footnote{I am not a lawyer. Do not rely on my legal advice.} In order to convert our algomusicological understanding into profit, we simply have to sue the composers
of every marginally successful musical work from now until they fix the law.

\section{Conclusion}

On one level, the puzzle posed by the title of this paper has a straightforward solution:
\begin{enumerate}
    \item Algomusicology
    \item Copyright litigation
    \item Profit!
\end{enumerate}

More seriously, this paper attempts to show that algomusicology can meaningfully shed light on how music works. Also, as in
the case of the \verb|copyright-infringement-song|, complexity theory can contribute ideas that are artistically interesting.
In other words, algomusicology can contribute to musical practice, not just explain it.

As an artform, music is an ideal subject for complexity analysis. Other media can also engage with complexity theory, but
few are as tractable to complexity theory's techniques. Sagan and Borges may have written fiction that explores the implications
of algorithmic creation, but it would be difficult to analyse 'Contact' and 'The Library of Babel' themselves through
Kolmogorov's lens.

Algomusicology is part of a rich tradition of information theoretical art and criticism. Music's deep structure and cultural
significance give algomusicology great potential as a source of artistic analysis and inspiration.

\appendix
\section{Encoding the Champernowne Constant}

It would not have been practical to discover the Gaye-Williams-Thicke constant by brute force. Playing through the song at normal
speed would have seen the expansion of the sun into a red giant interrupt the search before it made much progress.

Instead, I devised an encoding between the natural numbers and a piece of music. This allowed me to start with the music, encode
it and therefore discover the constant. Playing the Champernowne Constant as music is the reverse process.

The encoding is relatively direct. Each four digit word in the natural number is turned into a note, two digits for duration and
two digits for pitch. The two digits for duration are interpreted as a fraction, and the two digits for pitch as a midi code.

\begin{verbatim}
(fact
  (->> (phrase [2/3 1/3] [0 4]) encode)
    => 23001304)
\end{verbatim}

When there are multiple parts, they are multiplexed, based on which part is furthest behind. It would not be sufficient to
simply alternate between decoding a note of each part, as notes are not of uniform length. For example, in a two part piece
with a rapid melody and a slow, ostinato bass, the melody part of the encoding would slip further and further behind as
we alternately encoded one note from each part.

\begin{verbatim}
(fact
  (->> (phrase [1 1 1 1] [0 1 2 3])
       (with (phrase [2 2] [4 5]))
       encode)
    => 210411001101210511021103)
\end{verbatim}

Rests are encoded with zeros where duration normally is, leaving the other two digits for duration.
This is convenient because a zero duration is otherwise meaningless.

\begin{verbatim}
(fact
  (->> (phrase [1 1 1 1] [0 nil 2 nil]) encode)
    => 1100001111020011)
\end{verbatim}

For the Gayte-Williams-Thicke constant, I wrote a version of `Blurred Lines' with three parts and used a three part encoding.
The piece presented is actually lexicon for music of up to three parts using equal temperament and notes with simple
fractional durations, not all possible sound art pieces.

This is sufficient to clash with the vast majority of known works from a copyright perspective, though
it is not enough fideltity to reproduce the exact feel and playing style of every known piece. However, extending the idea of
a lexicon to binary formats like mp3 would be very possible.

The full source code for the \verb|copyright-infringement-song|, along with the rest of the code samples in this paper, is
available at \url{https://github.com/ctford/kolmogorov-music}.

\acks

Thank you to the StrangeLoop conference of 2015, which accepted the talk `Kolmogorov Music' from which this paper derives.

Thank you also to Sam Aaron and Jeff Rose for giving the world Overtone\cite{Overtone}, the audio programming environment
that made this paper possible.

Finally, thank you to the generous people who read through early drafts of this paper and made many valuable suggestions.

\bibliographystyle{plainnat}

% The bibliography should be embedded for final submission.

\begin{thebibliography}{}
\softraggedright

\bibitem{Overtone} Aaron, Sam and Rose, Jeff: Overtone, \url{https://overtone.github.io}.
\bibitem{Anti EP} Autechre (Rob Brown and Sean Booth): Anti EP, \textit{Warp Records}, 1994.
\bibitem{The Library of Babel} Borges, Jorge Luis: `The Library of Babel' in The Garden of Forking Paths, \textit{Editorial Sur}, 1941.
\bibitem{Gnomes} Brady, Pam, Parker, Trey and Stone, Matt: Gnomes, Episode 17, Season 2 of \textit{South Park}, 1998.
\bibitem{The construction of decimals normal in the scale of ten} Champernowne, David: The construction of decimals normal in the scale of ten, \textit{Journal of the London Mathematical Society}, volume 8, 1933.
\bibitem{Leipzig} Ford, Chris: Leipzig, \url{https://github.com/ctford/leipzig}.
\bibitem{Joseph Gallivan on pop} Gallivan, Joseph: Joseph Gallivan on pop, \textit{The Independent}, July 18, 1994.
\bibitem{Got to Give It Up} Gaye, Marvin: Got to Give It Up, \textit{Tamla}, 1977.
\bibitem{Clojure} Hickey, Rich: Clojure, \url{http://clojure.org/}
\bibitem{On Tables of Random Numbers}  Kolmogorov, Andrey: On Tables of Random Numbers, \textit{SankhyƒÅ Series A}, volume 25, 1963.
\bibitem{Midje} Marick, Brian: Midje, \url{https://github.com/marick/Midje}.
\bibitem{Analysis by Compression} Meredith, David: Analysis by Compression, \textit{The Music Encoding Conference}, 2013.
\bibitem{USA Today} Oldenburg, Ann: `Blurred Lines' jury finds for Marvin Gaye, \textit{USA Today}, March 11, 2015.
\bibitem{Contact} Sagan, Carl: Contact, \textit{Simon and Schuster}, 1985.
\bibitem{Propositions as Types} Wadler, Philip: Propositions as Types, \textit{Communications of the ACM}, volume 58, issue 12, 2015.
\bibitem{Blurred Lines} Williams, Pharrel and Thicke, Robin: Blurred Lines, \textit{Star Trak Recordings}, 2013.

\end{thebibliography}

\end{document}
